# TODO: cache search result

define (require) ->

	Fn = require 'hilib/functions/general'

	config = require 'config'
	token = require 'hilib/managers/token'

	Models =
		Search: require 'models/project/search'
		state: require 'models/state'

	Views =
		Base: require 'views/base'
		FacetedSearch: require 'faceted-search'
		Modal: require 'hilib/views/modal/main'

	Templates =
		Search: require 'text!html/project/main.html'
		Results: require 'text!html/project/results.html'

	class ProjectSearch extends Views.Base

		className: 'projectsearch'

		# ### Initialize
		initialize: ->
			super

			# The model of ProjectSearch holds the serverResponse generated by the FacetedSearch
			@model = new Models.Search()
			@listenTo @model, 'change', (model, options) =>
				# Reset the entries with the newly fetched results
				@project.get('entries').set model.get 'results'
				@listenTo @project.get('entries'), 'current:change', (entry) => @publish 'navigate:entry', entry.id
				
				@updateHeader()
				@renderResult()
				

			Models.state.getCurrentProject (project) =>
				### console.log 'callback called' # FIX Callback is called twice on login! But initialize is only run once ###
				@project = project
				@render()

		# ### Render
		render: ->
			rtpl = _.template Templates.Search, @project.attributes
			@$el.html rtpl

			@facetedSearch = new Views.FacetedSearch
				el: @$('.faceted-search-placeholder')
				baseUrl: config.baseUrl
				searchPath: 'projects/'+@project.id+'/search'
				token: token.get()
				textSearchOptions:
					textLayers: @project.get 'textLayers'
					searchInAnnotations: true
					searchInTranscriptions: true
				queryOptions:
					resultRows: 12
			@listenTo @facetedSearch, 'results:change', (response, queryOptions) => 
				@model.queryOptions = queryOptions
				@model.set response

			@

		renderResult: ->
			rtpl = _.template Templates.Results, model: @model
			@$('ul.entries').html rtpl
			
			if @model.queryOptions.term? and @model.queryOptions.term isnt ''
				document.getElementById('cb_showkeywords').checked = true
				@$('.keywords').show()
			else 
				document.getElementById('cb_showkeywords').checked = false
				@$('.keywords').hide()

			@

		# ### Events
		events:
			'click .submenu li[data-key="newsearch"]': 'newSearch'
			'click .submenu li[data-key="newentry"]': 'newEntry'
			'click li.entry label': 'changeCurrentEntry'
			'click .pagination li.prev': 'changePage'
			'click .pagination li.next': 'changePage'
			'click li[data-key="selectall"]': => Fn.checkCheckboxes '.entries input[type="checkbox"]', true, @el
			'click li[data-key="deselectall"]': => Fn.checkCheckboxes '.entries input[type="checkbox"]', false, @el
			'change #cb_showkeywords': 'toggleKeywords'

		newEntry: (ev) ->
			$html = $('<label>Name</label><input type="text" name="name" />')

			modal = new Views.Modal
				title: "Create a new entry"
				$html: $html
				submitValue: 'Create entry'
				width: '300px'
			modal.on 'submit', =>
				entries = @project.get('entries')
				
				modal.message 'success', 'Creating new entry...'
				
				@listenToOnce entries, 'add', (entry) =>
					modal.close()
					@publish 'navigate:entry', entry.id

				entries.create {name: modal.$('input[name="name"]').val()}, wait: true
					

		toggleKeywords: (ev) ->
			if ev.currentTarget.checked then @$('.keywords').show() else @$('.keywords').hide()

		changePage: (ev) ->
			ct = $(ev.currentTarget)
			return if ct.hasClass 'inactive'

			$('.pagination li').removeClass 'inactive'

			if ct.hasClass 'prev'
				@facetedSearch.prev()
			else if ct.hasClass 'next'
				@facetedSearch.next()


		changeCurrentEntry: (ev) ->
			entryID = ev.currentTarget.getAttribute 'data-id'
			@project.get('entries').setCurrent entryID
			# id = ev.currentTarget.id.replace 'entry', ''
			
			

		# ### Methods

		updateHeader: ->
			@$('h3.numfound').html @model.get('numFound') + ' letters found'
				
			currentpage = (@model.get('start') / @model.get('rows')) + 1
			pagecount = Math.ceil @model.get('numFound') / @model.get('rows')

			if pagecount > 1
				@$('.pagination li.prev').addClass 'inactive' if not @facetedSearch.hasPrev()
				@$('.pagination li.next').addClass 'inactive' if not @facetedSearch.hasNext()

				@$('.pagination li.currentpage').html currentpage
				@$('.pagination li.pagecount').html pagecount

				@$('.pagination').show()
			else
				@$('.pagination').hide()
# TODO: cache search result

define (require) ->

	Fn = require 'hilib/functions/general'

	config = require 'config'
	token = require 'hilib/managers/token'

	Models =
		Search: require 'models/project/search'
		# state: require 'models/state'

	Collections = 
		projects: require 'collections/projects'

	Views =
		Base: require 'views/base'
		FacetedSearch: require 'faceted-search'
		Modal: require 'hilib/views/modal/main'
		EditSelection: require 'views/project/editselection'

	Templates =
		Search: require 'text!html/project/main.html'
		Results: require 'text!html/project/results.html'

	class ProjectSearch extends Views.Base

		className: 'projectsearch'

		# ### Initialize
		initialize: ->
			super

			# The model of ProjectSearch holds the serverResponse generated by the FacetedSearch
			@model = new Models.Search()
			@listenTo @model, 'change', (model, options) =>
				# Reset the entries with the newly fetched results
				@project.get('entries').set model.get 'results'
				@listenTo @project.get('entries'), 'current:change', (entry) =>
					Backbone.history.navigate "projects/#{@project.get('name')}/entries/#{entry.id}", trigger: true
				
				@updateHeader()
				@renderResult()
				

			# Models.state.getCurrentProject (project) =>
			# 	### console.log 'callback called' # FIX Callback is called twice on login! But initialize is only run once ###
			# 	@project = project
			Collections.projects.getCurrent (@project) => @render()

		# ### Render
		render: ->
			rtpl = _.template Templates.Search, @project.attributes
			@$el.html rtpl

			# Render the EditSelection view. Is toggled in the menu and can be used
			# to edit the metadata of multiple entries at once.
			@editSelection = new Views.EditSelection
				el: @el.querySelector '.editselection-placeholder'
				model: @project
			@listenTo @editSelection, 'close', @uncheckCheckboxes

			@facetedSearch = new Views.FacetedSearch
				el: @$('.faceted-search-placeholder')
				baseUrl: config.baseUrl
				searchPath: 'projects/'+@project.id+'/search'
				token: token.get()
				textSearchOptions:
					textLayers: @project.get 'textLayers'
					searchInAnnotations: true
					searchInTranscriptions: true
				queryOptions:
					resultRows: 12
			@listenTo @facetedSearch, 'unauthorized', => @publish 'unauthorized'
			@listenTo @facetedSearch, 'results:change', (response, queryOptions) => 
				@model.queryOptions = queryOptions
				@model.set response

			@

		renderResult: ->
			rtpl = _.template Templates.Results, 
				model: @model
				generateID: Fn.generateID
			@$('ul.entries').html rtpl
			
			if @model.queryOptions.term? and @model.queryOptions.term isnt ''
				document.getElementById('cb_showkeywords').checked = true
				@$('.keywords').show()
			else 
				document.getElementById('cb_showkeywords').checked = false
				@$('.keywords').hide()

			@

		# ### Events
		events:
			'click .submenu li[data-key="newsearch"]': -> @facetedSearch.reset()
			'click .submenu li[data-key="newentry"]': 'newEntry'
			'click .submenu li[data-key="editselection"]': 'showEditMetadata'
			'click .submenu li[data-key="publish"]': 'publishProject'
			'click li.entry label': 'changeCurrentEntry'
			'click .pagination li.prev': 'changePage'
			'click .pagination li.next': 'changePage'
			'click li[data-key="selectall"]': -> Fn.checkCheckboxes '.entries input[type="checkbox"]', true, @el
			'click li[data-key="deselectall"]': 'uncheckCheckboxes'
			'change #cb_showkeywords': (ev) -> if ev.currentTarget.checked then @$('.keywords').show() else @$('.keywords').hide()
			'change .entry input[type="checkbox"]': -> @editSelection.toggleInactive()

		publishProject: (ev) ->
			busyText = 'Publishing...'
			return false if ev.currentTarget.innerHTML is busyText
			ev.currentTarget.innerHTML = busyText
			ev.currentTarget.classList.add 'active'
			
			@project.createDraft =>
				ev.currentTarget.innerHTML = 'Publish'
				ev.currentTarget.classList.remove 'active'

		showEditMetadata: (ev) ->
			# show hide checkboxes
			# @$('.editselection-placeholder').toggle()

			editmetadataPlaceholder = @el.querySelector('.editselection-placeholder')

			visible = editmetadataPlaceholder.style.display is 'block'

			display = if visible then 'none' else 'block'
			opacity = if visible then 0 else 1

			editmetadataPlaceholder.style.display = display
			checkboxes = @el.querySelectorAll('ul.entries input[type="checkbox"]')
			cb.style.opacity = opacity for cb in checkboxes

			# When hiding edit metadata, uncheck all checkboxes
			Fn.checkCheckboxes null, false if visible

		newEntry: (ev) ->
			$html = $('<form><ul><li><label>Name</label><input type="text" name="name" /></li></ul></form>')

			modal = new Views.Modal
				title: "Create a new entry"
				$html: $html
				submitValue: 'Create entry'
				width: '300px'
			modal.on 'submit', =>
				entries = @project.get('entries')
				
				modal.message 'success', 'Creating new entry...'
				
				@listenToOnce entries, 'add', (entry) =>
					modal.close()
					@publish 'message', 'New entry added to project.'
					Backbone.history.navigate "projects/#{@project.get('name')}/entries/#{entry.id}", trigger: true

				entries.create {name: modal.$('input[name="name"]').val()}, wait: true

		changePage: (ev) ->
			ct = $(ev.currentTarget)
			return if ct.hasClass 'inactive'

			$('.pagination li').removeClass 'inactive'

			if ct.hasClass 'prev'
				@facetedSearch.prev()
			else if ct.hasClass 'next'
				@facetedSearch.next()


		changeCurrentEntry: (ev) ->
			# Only change current entry if the edit metadata isn't active.
			unless @el.querySelector('.editselection-placeholder').style.display is 'block'
				entryID = ev.currentTarget.getAttribute 'data-id'
				@project.get('entries').setCurrent entryID
				# id = ev.currentTarget.id.replace 'entry', ''
			
			

		# ### Methods

		uncheckCheckboxes: -> Fn.checkCheckboxes '.entries input[type="checkbox"]', false, @el

		# * TODO: called several times!
		updateHeader: ->
			@$('.pagination li.next').removeClass 'inactive' # TMP!
			@$('h3.numfound').html @model.get('numFound') + ' entries found'
				
			currentpage = (@model.get('start') / @model.get('rows')) + 1
			pagecount = Math.ceil @model.get('numFound') / @model.get('rows')

			if pagecount > 1
				@$('.pagination li.prev').addClass 'inactive' if not @facetedSearch.hasPrev()
				@$('.pagination li.next').addClass 'inactive' if not @facetedSearch.hasNext()

				@$('.pagination li.currentpage').html currentpage
				@$('.pagination li.pagecount').html pagecount

				@$('.pagination').show()
			else
				@$('.pagination').hide()